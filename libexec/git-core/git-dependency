#!/usr/bin/perl

# Git package manager plugin.
#
# Command syntax
#-----------------
#
# git-dependency add [ -c | --commit ] [ -m | --message {message} ] {repository} ...
#                list
#                remove [ -c | --commit ] [ -m | --message {message} ] [ {path} ... ]
#
# Repository files
#-------------------
#
# .gitpackage
#
#  Contains package information for the repository.
#
#  [dependency "dependency/path"]
#    url = ??/repository.git
#    path = dependency/path
#
# Description
#--------------
#
#  This git command provides a package manager interface to the git submodule
#  command structure.  Packages are libraries that are included in application
#  source directories.  Some packages depend upon other packages.  But the 
#  packages that are depended upon do not reside in a sub directory of the
#  original package.  Packages are treated as submodules, but due to limitations
#  with the handling of dependencies in the submodule commands, this command
#  library was written.
#
# Command overview
#
# [ add {repository} ]
#
#  The add dependency command includes a git package in your application source
#  directory.  As the package is added and pulled, this script checks if a 
#  .gitpackage file exists, and if so, adds and pulls all of the dependent
#  packages into the source directory.  It performs this add in a recursive
#  manner until all dependent packages have been added and pulled.  After it
#  pulls all of the packages needed by the specified package, it initializes
#  the submodules.
#
#    Parameters
#    ------------
#    1. {repository} - Url or path of the git submodule repository.
#
# [ list ]
#
#  The list package command lists all of the packages currently included in the
#  git repository.
#
# [ remove [ {path} ... ] ]
#
#   The remove dependency command removes a dependency from this submodule 
#   repository.

#*******************************************************************************
#-------------------------------------------------------------------------------
# Command imports
#-------------------------------------------------------------------------------

use strict;
use warnings;

use constant {
	# Boolean syntactic sugar
	TRUE  => 1,
	FALSE => 0,
	# Commands
	CMD_ADD    => 'add',
	CMD_LIST   => 'list',
	CMD_REMOVE => 'remove',
	# Files
	FILE_PACKAGE => '.gitpackage',
	# Settings
	COMMAND        => 'command',
	REPO_PATH      => 'repo_path',
	VERBOSE        => 'verbose',
	COMMIT_FLAG    => 'commit',
	COMMIT_MSG     => 'message',
	INPUT          => 'input',
	DEPENDENCY_MAP => 'dependency_map',
	REMOVE_ALL     => 'remove_all',	
};

use Switch;

# CPAN modules

use Getopt::OO;

# Git modules.

use Git;

#*******************************************************************************
#-------------------------------------------------------------------------------
# Command variables
#-------------------------------------------------------------------------------

# Repository information. ( Dies if no repository! )
our $REPO = Git->repository();

# Command settings.
our %SETTINGS = (
	COMMAND        => shift @ARGV,
	PARAMETERS     => \@ARGV,
	REPO_PATH      => $REPO->wc_path(),
	DEPENDENCY_MAP => {},
);

#*******************************************************************************
#-------------------------------------------------------------------------------
# Command initialization
#-------------------------------------------------------------------------------

switch ($SETTINGS{COMMAND}) {
	
	case CMD_ADD {
		command_add(); # Launch dependency add command	
	}
	case CMD_LIST {
		command_list(); # Launch dependency list command
	}
	case CMD_REMOVE {
		command_remove(); # Launch dependency remove command
	}
	else {
		display_usage();	
	}	
}

#*******************************************************************************
#-------------------------------------------------------------------------------
# Command help display
#-------------------------------------------------------------------------------

sub display_usage {
	display(
		($SETTINGS{COMMAND} ? 'Invalid command specified.' 
							: 'No command specified.'),
		'',
		'Supported dependency commands :',
		'',
		' git dependency add --help',
		' git dependency list --help',
		' git dependency remove --help'
	);
}

#*******************************************************************************
#-------------------------------------------------------------------------------
# Dependency ADD command
#-------------------------------------------------------------------------------

# Command syntax
#-----------------
#
# git-dependency add
#  [ -h | --help ]
#  [ -v | --verbose ] 
#  [ -c | --commit ] 
#  [ -m | --message {message} ] 
#  {repository} ...

sub command_add {
	
	# Parse command settings.
	command_add_settings(
		Getopt::OO->new($SETTINGS{PARAMETERS}, command_add_options())
	);
		
	verbose('Starting dependency add command.');
	verbose('Repository path: ' . $SETTINGS{REPO_PATH});
	
	# Load existing dependencies for this package.
	load_dependencies();	
	
	# Add dependencies to .gitpackage file (overwrite if exists).
	foreach my $repo (@{$SETTINGS{INPUT}}) {
		my $path = find_repository_path($repo);
		
		verbose("Resolved path of package [ $repo ] to [ $path ]");
		
		$SETTINGS{DEPENDENCY_MAP}->{$path} = {
			path => $path,
			url =>  $repo,			
		}
	}
	
	# Store modified dependencies for this package.
	store_dependencies();
	
	# Commit changes to the package file.
	commit_changes();			
}

#-------------------------------------------------------------------------------

sub command_add_options {
	
	my %options = (
		other_values => {
    	help => 'repository ...',
		},
	);
	
	commit_options(\%options);
	display_options(\%options);
	help_options(\%options);
	
	return %options;	
}

#-------------------------------------------------------------------------------

sub command_add_settings {
	my $parser = shift;
	
	# Abort and show help information if requested.
	parse_help_settings($parser);
	
	# Get command repositories.
	if (!@{$parser->Values('other_values')}) {
		display($parser->Help());
		exit();
	}	
	
	@{$SETTINGS{INPUT}} = $parser->Values('other_values');
	
	# Get extra command options.
	parse_display_settings($parser);
	parse_commit_settings($parser);	
}

#*******************************************************************************
#-------------------------------------------------------------------------------
# Dependency LIST command
#-------------------------------------------------------------------------------

# Command syntax
#-----------------
#
# git-dependency list
#  [ -h | --help ]
#  [ -v | --verbose ] 

sub command_list {
	
	# Parse command settings.
	command_list_settings(
		Getopt::OO->new($SETTINGS{PARAMETERS}, command_list_options())
	);
	
	verbose('Starting dependency list command.');
	verbose('Repository path: ' . $SETTINGS{REPO_PATH});
	
	# Load existing dependencies for this package.
	load_dependencies();
	
	# Print representation of dependencies for this package.
	display_dependencies();			
}

#-------------------------------------------------------------------------------

sub command_list_options {
	
	my %options = ();
			
	display_options(\%options);
	help_options(\%options);
		
	return %options;	
}

#-------------------------------------------------------------------------------

sub command_list_settings {
	my $parser = shift;
	
	# Get extra command options.
	parse_help_settings($parser);
	parse_display_settings($parser);
}

#*******************************************************************************
#-------------------------------------------------------------------------------
# Dependency REMOVE command
#-------------------------------------------------------------------------------

# Command syntax
#-----------------
#
# git-dependency remove
#  [ -h | --help ]
#  [ -v | --verbose ]  
#  [ -c | --commit ] 
#  [ -m | --message {message} ] 
#  [ {path} ... ]

sub command_remove {
	
	# Parse command settings.
	command_remove_settings(
		Getopt::OO->new($SETTINGS{PARAMETERS}, command_remove_options())
	);
	
	verbose('Starting dependency remove command.');
	verbose('Repository path: ' . $SETTINGS{REPO_PATH});
	
	# Load existing dependencies for this package.
	load_dependencies();
	
	if ($SETTINGS{REMOVE_ALL}) {
		%{$SETTINGS{DEPENDENCY_MAP}} = ();
	}
	elsif ($SETTINGS{INPUT}) {
		# Remove all specified paths.
		foreach my $path (@{$SETTINGS{INPUT}}) {		
			delete $SETTINGS{DEPENDENCY_MAP}->{$path};	
		}
	}
	
	# Store modified dependencies for this package.
	store_dependencies();
	
	# If specified, commit changes to the package file.
	commit_changes();		
}

#-------------------------------------------------------------------------------

sub command_remove_options {
	
	my %options = (
		'-a' => {
			help => 'Remove all dependencies.',
		},
		'--all' => {
			help => 'See information for -a option.',	
		},
		'other_values' => {
        	help => 'path ...',
        },
	);
		
	commit_options(\%options);
	display_options(\%options);
	help_options(\%options);
	
	return %options;	
}

#-------------------------------------------------------------------------------

sub command_remove_settings {
	my $parser = shift;
	
	# Abort and show help information if requested.
	parse_help_settings($parser);
	
	# Get command paths.
	if (@{$parser->Values('other_values')}) {
		$SETTINGS{INPUT} = $parser->Values('other_values');
	}
	
	$SETTINGS{REMOVE_ALL} = $parser->Values('-a') || $parser->Values('--all');
	
	if (!$SETTINGS{INPUT} && !$SETTINGS{REMOVE_ALL}) {
		display();
		display('You should either specify packages' 
				. ' or the --all option to remove all dependencies.');
		display();
		display($parser->Help());	
	}
	
	# Get extra command options.
	parse_display_settings($parser);
	parse_commit_settings($parser);	
}

#*******************************************************************************
#-------------------------------------------------------------------------------
# Internal utility functions
#-------------------------------------------------------------------------------

sub help_options {
	
	# Add to pre-existing options hash.
	my $options = shift;
	
	$options->{'-h'} = {
		help => 'Display help information.',	
	};
	$options->{'--help'} = {
		help => 'See information for -h option.',
	};
}

#-------------------------------------------------------------------------------

sub parse_help_settings {
	my $parser = shift;
	
	if ($parser->Values('-h') || $parser->Values('--help')) {
		display($parser->Help());
		exit();
	}	
}

#-------------------------------------------------------------------------------

sub display_options {
	
	# Add to pre-existing options hash.
	my $options = shift;
	
	$options->{'-v'} = {
       	help => 'Display more information.',
    };
    $options->{'--verbose'} = {
    	help => 'See information for -v option.',
    };	
}

#-------------------------------------------------------------------------------

sub parse_display_settings {
	my $parser = shift;
	
	unless($parser->Values()) {
		return;
	}
	
	# Parse settings.
	$SETTINGS{VERBOSE} = $parser->Values('-v') || $parser->Values('--verbose');
}

#-------------------------------------------------------------------------------

sub commit_options  {
	
	# Add to pre-existing options hash.
	my $options = shift;
	
	# Commmit flag
	$options->{'-c'} = {
		help => 'Commit after this operation.',
	};
	$options->{'--commit'} = {
		help => 'See information for -c option.'
	};
	
	# Commit message
	$options->{'-m'} = {
		help => 'See information for --message option.',
		n_values => 1,
	};
	$options->{'--message'} = {
		help => [
			'If you are commiting after this add dependency operation,',
			'you may specify the committ message.   If no commit message',
			'is given, then an editor is displayed.',
			'This is similar to the commit command.',
		],
		n_values => 1,
	};	
}

#-------------------------------------------------------------------------------

sub parse_commit_settings {
	my $parser = shift;
	
	unless ($parser->Values()) {
		return;
	}
	
	# Parse settings.
	$SETTINGS{COMMIT_FLAG} = $parser->Values('-c') 
							|| $parser->Values('--commit');
							
	if ($parser->Values('-m')) {
		$SETTINGS{COMMIT_MSG} = $parser->Values('-m');
	}
	elsif ($parser->Values('--message')) {
		$SETTINGS{COMMIT_MSG} = $parser->Values('--message');	
	}	
}

#-------------------------------------------------------------------------------

sub display_dependencies {
	my %packages       = ();
	my $display_length = 0;
	
	# Gather info about current packages.
	foreach my $package (keys %{$SETTINGS{DEPENDENCY_MAP}}) {
		$packages{$package} = $SETTINGS{DEPENDENCY_MAP}->{$package}{url};
		$display_length = (length $package > $display_length ? length $package 
															 : $display_length);		
	}
	
	# Display current packages.
	if ($display_length) {
		display();
	
		foreach (sort keys %packages) {
			display(sprintf " %-${display_length}s  [  %s  ]", 
					$_, $packages{$_});
		}
	}
	else {
		display('No dependencies registered.');
	}
}

#-------------------------------------------------------------------------------

sub load_dependencies {
	
	# Initialize dependency map for current repo.
	my $package_file = $SETTINGS{REPO_PATH} . FILE_PACKAGE;
		
	verbose('Loading package file : ' . $package_file);
	open(HANDLE, $package_file) or return;
	
	verbose('Package file opened successfully.');
	
	# Import packages.
	my $package;
	
	while (<HANDLE>) {
		# Strip whitespace.
		s/\s*//g;
		
		next unless ($_);
			
		# If we come across a new package, change packages.
		if (/^\[dependency"?([^"\]]+)"?\]$/i) {
			$package = $1;
			verbose('Loading package : ' . $package);	
		}
		elsif (/^\[.+\]$/) {
			$package = FALSE;
		}
		elsif ($package) {
				
			# Split variable and value on equals sign.
			my ($variable, $value) = split(/\=/);
			
			verbose("Setting package variable [ $variable ]"
					. " to value [ $value ]");
			
			$SETTINGS{DEPENDENCY_MAP}->{$package}{$variable} = $value; 
		}	
	}
	
	verbose('Dependencies loaded successfully.');
	close(HANDLE);
}

#-------------------------------------------------------------------------------

sub store_dependencies {
	
	# Store dependency map for current repo.
	my $package_file = $SETTINGS{REPO_PATH} . FILE_PACKAGE;
	my @lines = load_exclude_info($package_file, 'dependency');
	
	if (! @lines && ! keys %{$SETTINGS{DEPENDENCY_MAP}}) {
		verbose('Removing package file : ' . $package_file);
		unlink $package_file;
		return;		
	}
			
	verbose('Writing package file : ' . $package_file);
	open(HANDLE, ">$package_file") or die $!;
	
	verbose('Package file opened successfully.');
	
	# Always write in same order. (for versioning)
	foreach my $package (sort keys %{$SETTINGS{DEPENDENCY_MAP}}) {
		
		push(@lines, "[dependency \"$package\"]\n");
		
		my $variables = $SETTINGS{DEPENDENCY_MAP}->{$package};
		foreach (sort keys %$variables) {
			push(@lines, "  $_ = " . $variables->{$_} . "\n");	
		}			
	}
	
	print HANDLE @lines;	
	
	verbose('Dependencies saved successfully.');
	close(HANDLE);
}

#-------------------------------------------------------------------------------

sub load_exclude_info {
	my ($filename, $exclude) = @_;
	
	my @lines   = ();
	my $include = TRUE;
	
	open HANDLE, $filename or return;

	while (<HANDLE>) {
		my $line = $_;
			
		# Strip whitespace.
		s/\s*//g;
		
		next unless ($_);		
			
		if (/^\[$exclude.+\]$/i) {
			$include = FALSE;	
		}
		elsif (/^\[.+\]$/) {
			$include = TRUE;
			push(@lines, $line);	
		}
		elsif ($include) {
			push(@lines, $line);	
		}		
	}	
	close HANDLE;
	
	return @lines;	
}

#-------------------------------------------------------------------------------

sub find_repository_path {
	my @repo_parts = split(/\//, shift);
	
	# Remove everything but the file name.
	my $path = pop(@repo_parts);
	
	# Remove anything before the last dash in the file name.
	$path =~ s/^([^-]*-)*//;
	
	# Remove the git extension from the file.
	$path =~ s/\.git$//i;
	
	# Replace dot separators with forward slashes.
	$path =~ s/\./\//g;
	
	return $path;	
}

#-------------------------------------------------------------------------------

sub commit_changes {
	if ($SETTINGS{COMMIT_FLAG}) {
		# Add the package file to the staged changes.
		$REPO->command('add', $SETTINGS{REPO_PATH} . FILE_PACKAGE);
		
		# Commit changes.
		if ($SETTINGS{COMMIT_MSG}) {
			$REPO->command('commit', '-m ' . $SETTINGS{COMMIT_MSG});	
		}
		else {
			# Run this through system, since it is an interactive command.
			system('git commit');
		}
	}
}

#-------------------------------------------------------------------------------

sub display {
	# If no input given, assume newline.
	unless (@_) {
		print "\n";
		return;
	}
	
	# If more than one parameter, assume multiple line text as array.
	if (@_ > 1) {
		foreach my $line (@_) {
			print $line . "\n";
		}
	}
	# If single parameter, print one line.
	else {
		print $_[0] . "\n";
	}	
}

#-------------------------------------------------------------------------------

sub verbose {
	# Only print if verbose flag was set.
	if ($SETTINGS{VERBOSE}) {
		display(@_);		
	}	
}
